
task idlToJava(type: IdlToJava) {
    include "${rootDir}/idl-includes/ONE"
    include "${rootDir}/idl-includes/TWO"

    jacorbClasspath = files("/path/to/jacorb.jar")

    outputDirectory = file("${buildDir}/idl/java")
}


@CacheableTask
public class IdlToJava extends DefaultTask {
    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)
    FileCollection idlFiles

    @Classpath
    FileCollection jacorbClasspath

    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)
    FileCollection includedFiles = project.objects.fileCollection()

    private List<String> includes = []

    void include(String path) {
        File includeDir = project.file(path)
        includes << project.relativePath(includeDir)
        includedFiles.from project.fileTree(includeDir)
    }

    @OutputDirectory
    File outputDirectory

    IdlToJava() {
        description = 'Create .java files from selected IDL files'
    }

    @TaskAction
    void processIdl() {
        // Create batches to send to the JacORB compiler
        int count = 0
        FileCollection batch = project.files()
        idlFiles.each { File idlFile->
            count++
            batch += project.files(idlFile)
            if (count == 7) {
                compileBatch(batch)
                count = 0
                batch = project.files()
            }
        }
        if (count > 0) {
            compileBatch(batch)
        }
    }
    private void compileBatch(def idlFiles) {
        // DUMMY IMPLEMENTATION
        project.mkdir(outputDirectory)
        project.copy {
            from idlFiles
            into outputDirectory
        }
        println includes.collect {it}
        println includedFiles.collect {it.path}
        println idlFiles.collect {it.path}
        return

        // REAL IMPLEMENTATION
        // Compile IDL to Java using JacORB compiler
        project.javaexec {
            main = 'org.jacorb.idl.parser'
            classpath = jacorbClasspath
            args '-DNBU_USES_JACORB_FOR_COMPILATION'
            includes.each { include ->
                args "-I${include}"
            }
            args '-d',"${outputDirectory}"
            args '-sloppy_forward'
            idlFiles.each {
                File idlFile ->
                    args "${idlFile}"
            }
        }
    }
}
